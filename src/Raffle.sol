// 1ï¸âƒ£ ç‰ˆæœ¬å£°æ˜
// 2ï¸âƒ£ ä¾èµ–å¯¼å…¥ï¼ˆImportsï¼‰
// 3ï¸âƒ£ äº‹ä»¶ï¼ˆEventsï¼‰
// 3ï¸âƒ£ è‡ªå®šä¹‰é”™è¯¯ï¼ˆErrorsï¼‰
// 4ï¸âƒ£ æ¥å£ã€åº“ã€åˆçº¦ï¼ˆInterfaces, Libraries, Contractsï¼‰

// ğŸ”½ åˆçº¦å†…éƒ¨å¸ƒå±€ ğŸ”½
// 1ï¸âƒ£ ç±»å‹å£°æ˜ï¼ˆType declarationsï¼‰
// 2ï¸âƒ£ çŠ¶æ€å˜é‡ï¼ˆState variablesï¼‰
// 3ï¸âƒ£ äº‹ä»¶ï¼ˆEventsï¼‰
// 4ï¸âƒ£ è‡ªå®šä¹‰é”™è¯¯ï¼ˆErrorsï¼‰
// 5ï¸âƒ£ ä¿®é¥°ç¬¦ï¼ˆModifiersï¼‰
// 6ï¸âƒ£ å‡½æ•°ï¼ˆFunctionsï¼‰

// ğŸ”½ å‡½æ•°çš„æ¨èæ’åº ğŸ”½
// 1ï¸âƒ£ æ„é€ å‡½æ•°ï¼ˆconstructorï¼‰
// 2ï¸âƒ£ receive() å‡½æ•°ï¼ˆå¦‚æœå­˜åœ¨ï¼‰
// 3ï¸âƒ£ fallback() å‡½æ•°ï¼ˆå¦‚æœå­˜åœ¨ï¼‰
// 4ï¸âƒ£ external å‡½æ•°
// 5ï¸âƒ£ public å‡½æ•°
// 6ï¸âƒ£ internal å‡½æ•°
// 7ï¸âƒ£ private å‡½æ•°
// 8ï¸âƒ£ è§†å›¾ï¼ˆviewï¼‰ & çº¯å‡½æ•°ï¼ˆpureï¼‰

// SPDX-License-Identifier: MIT
pragma solidity ^0.8.28;

import {VRFConsumerBaseV2Plus} from "@chainlink/contracts/src/v0.8/vrf/dev/VRFConsumerBaseV2Plus.sol";
import {VRFV2PlusClient} from "@chainlink/contracts/src/v0.8/vrf/dev/libraries/VRFV2PlusClient.sol";
import {AutomationCompatibleInterface} from
    "@chainlink/contracts/src/v0.8/automation/interfaces/AutomationCompatibleInterface.sol";

/**
 * @title ä¸€ä¸ªæŠ½å¥–åˆçº¦ç¤ºä¾‹
 * @author cyc
 * @notice æœ¬åˆçº¦ç”¨äºåˆ›å»ºä¸€ä¸ªå½©ç¥¨æ™ºèƒ½åˆçº¦
 * @dev æœ¬åˆçº¦å®ç°äº† Chinklink VRF éšæœºæ•°ç”Ÿæˆå™¨ï¼ŒChinklink Automation å®šæ—¶å™¨
 */
contract Raffle is VRFConsumerBaseV2Plus, AutomationCompatibleInterface {
    enum RaffleState {
        OPEN,
        CALCULATING
    }

    // Chainlink VRF ç›¸å…³å˜é‡
    bytes32 private immutable i_gasLane;
    uint256 private immutable i_subscriptionId;
    uint16 private constant REQUEST_CONFIRMATIONS = 3;
    uint32 private immutable i_callbackGasLimit;
    uint32 private constant NUM_WORDS = 1;
    bool private constant ENABLE_NATIVE_PAYMENT = false;

    // Raffle ç›¸å…³å˜é‡
    uint256 private immutable i_entranceFee;
    // æŠ½å¥–æŒç»­æ—¶é—´
    uint256 private immutable i_interval;
    address payable[] private s_players;
    // è®°å½•æŠ½å¥–å¼€å§‹æ—¶é—´
    uint256 private s_lastTimeStamp;
    address private s_recentWinner;
    RaffleState private s_raffleState;

    event Raffle__EnterRaffle(address indexed player);
    event Raffle__PickedWinner(address winner);
    event Raffle__RequestedRaffleWinner(uint256 indexed requestId);
    event Raffle__WinnerCannotReceiveETH(address indexed winner, uint256 amount);

    error Raffle__NotEnoughETHSent();
    error Raffle__TransferFailed();
    error Raffle__RaffleNotOpen();
    error Raffle__UpkeepNotNeeded(uint256 currentBalance, uint256 numPlayers, uint256 raffleState);

    constructor(
        uint256 entranceFee,
        uint256 interval,
        address vrfCoordinator,
        bytes32 gasLane,
        uint256 subscriptionId,
        uint32 callbackGasLimit
    ) VRFConsumerBaseV2Plus(vrfCoordinator) {
        i_interval = interval;
        i_entranceFee = entranceFee;
        s_lastTimeStamp = block.timestamp;
        s_raffleState = RaffleState.OPEN;

        i_gasLane = gasLane;
        i_subscriptionId = subscriptionId;
        i_callbackGasLimit = callbackGasLimit;
    }

    function enterRaffle() external payable {
        // require(msg.sender >= i_entranceFee, "Not enough ETH!");
        if (msg.value < i_entranceFee) revert Raffle__NotEnoughETHSent();
        if (s_raffleState != RaffleState.OPEN) revert Raffle__RaffleNotOpen();
        s_players.push(payable(msg.sender));
        // emit event
        emit Raffle__EnterRaffle(msg.sender);
    }

    // å‘ VFR è¯·æ±‚éšæœºæ•°ï¼Œç¬¦åˆæ¡ä»¶åå°è£… VRFV2PlusClient.RandomWordsRequestï¼Œè¯·æ±‚éšæœºæ•°
    function performUpkeep(bytes calldata /* performData */ ) external override {
        (bool upkeepNeeded,) = checkUpkeep("");
        if (!upkeepNeeded) {
            revert Raffle__UpkeepNotNeeded(address(this).balance, s_players.length, uint256(s_raffleState));
        }

        s_raffleState = RaffleState.CALCULATING;
        uint256 requestId = s_vrfCoordinator.requestRandomWords(
            VRFV2PlusClient.RandomWordsRequest({
                keyHash: i_gasLane,
                subId: i_subscriptionId,
                requestConfirmations: REQUEST_CONFIRMATIONS,
                callbackGasLimit: i_callbackGasLimit,
                numWords: NUM_WORDS,
                extraArgs: VRFV2PlusClient._argsToBytes(VRFV2PlusClient.ExtraArgsV1({nativePayment: ENABLE_NATIVE_PAYMENT}))
            })
        );

        emit Raffle__RequestedRaffleWinner(requestId);
    }

    /**
     * @dev è¯¥å‡½æ•°ç”± Chainlink Keeper èŠ‚ç‚¹è°ƒç”¨ï¼Œ
     * å®ƒä¼šæ£€æŸ¥ `upkeepNeeded` æ˜¯å¦è¿”å› trueã€‚
     * éœ€è¦æ»¡è¶³ä»¥ä¸‹æ¡ä»¶ä¹‹ä¸€æ‰èƒ½è¿”å› trueï¼š
     * 1. æŠ½å¥–çš„æ—¶é—´é—´éš”å·²è¿‡å» â³ã€‚
     * 2. æŠ½å¥–ä»ç„¶å¼€æ”¾ ğŸŸï¸ã€‚
     * 3. åˆçº¦æœ‰ ETH ğŸ’°ã€‚
     * 4. è‡³å°‘æœ‰ä¸€ä½å‚ä¸è€… ğŸƒâ€â™‚ï¸ã€‚
     * 5. ä½ çš„è®¢é˜…ä¸­æœ‰è¶³å¤Ÿçš„ LINK èµ„é‡‘ ğŸ”—ã€‚
     */
    function checkUpkeep(bytes memory /* checkData */ )
        public
        view
        override
        returns (bool upkeepNeeded, bytes memory /* performData */ )
    {
        bool isOpen = RaffleState.OPEN == s_raffleState;
        bool timePassed = (block.timestamp - s_lastTimeStamp) > i_interval;
        bool hasPlayers = s_players.length > 0;
        bool hasBalance = address(this).balance > 0;
        upkeepNeeded = (isOpen && timePassed && hasPlayers && hasBalance);
        return (upkeepNeeded, "0x0");
    }

    // VRF å›è°ƒå‡½æ•°ï¼Œå¯¹éšæœºæ•°çš„å¤„ç†æ“ä½œ
    function fulfillRandomWords(
        uint256,
        /*requestId*/
        uint256[] calldata randomWords
    ) internal override {
        uint256 winnerIndex = randomWords[0] % s_players.length;
        address payable winner = s_players[winnerIndex];

        s_recentWinner = winner;
        s_raffleState = RaffleState.OPEN;

        s_players = new address payable[](0);
        s_lastTimeStamp = block.timestamp;

        emit Raffle__PickedWinner(winner);

        // å‘é€å¥–é‡‘
        (bool success,) = winner.call{value: address(this).balance}("");
        if (!success) {
            // ç”¨äºæµ‹è¯•
            emit Raffle__WinnerCannotReceiveETH(winner, address(this).balance);
            // å‘é€å¤±è´¥ï¼ŒæŠ›å‡ºå¼‚å¸¸
            revert Raffle__TransferFailed();
        }
    }

    function getEntranceFee() external view returns (uint256) {
        return i_entranceFee;
    }

    function getRaffleState() external view returns (RaffleState) {
        return s_raffleState;
    }

    function getPlayers() external view returns (address payable[] memory) {
        return s_players;
    }

    function getLastTimeStamp() external view returns (uint256) {
        return s_lastTimeStamp;
    }

    function getRecentWinner() external view returns (address) {
        return s_recentWinner;
    }
}
